PREFIX sp: <tag:stardog:api:property:>
PREFIX obo: <http://purl.obolibrary.org/obo/>


SELECT DISTINCT ?term ?termLabel ?parentTerm ?parentLabel ?rootClass ?rootLabel (<http://purl.obolibrary.org/obo/BFO_0000137> as ?hieProp)
FROM <http://www.pitt.edu/obc/ide/apollo-sv>
FROM <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
FROM <http://www.pitt.edu/obc/ide/article-instances>
WHERE {
    ?class rdfs:subClassOf* ?rootClass .
    ?rootClass <http://www.pitt.edu/obc/IDE_0000000024> ?isRoot .
  	?rootClass rdfs:label ?rootLabel .
    ?class rdfs:label ?classLabel .
  	?rootClass <http://www.pitt.edu/obc/IDE_0000001000> <http://purl.obolibrary.org/obo/BFO_0000137> .


  	{
    	?article obo:IAO_0000136 ?term .
    } UNION {
    	?article obo:IAO_0000136 [
        	<http://purl.obolibrary.org/obo/BFO_0000137> ?term ] .
    } UNION {
    	?article obo:IAO_0000136 [
          <http://www.obofoundry.org/ro/ro.owl#has_participant>*/<http://purl.obolibrary.org/obo/BFO_0000137>*/<http://www.obofoundry.org/ro/ro.owl#located_in> ?term ] .
    } 

  	?article rdf:type ?articleClass .
    ?articleClass rdfs:subClassOf+ <http://www.pitt.edu/obc/IDE_0000000025> .
    ?term rdfs:label ?termLabel ;
        rdf:type ?class .

  	OPTIONAL {
        ?term <http://purl.obolibrary.org/obo/BFO_0000137> ?parentTerm .
  		?parentTerm rdfs:label ?parentLabel .

  		?parentTerm <http://purl.obolibrary.org/obo/BFO_0000137>*/rdf:type/rdfs:subClassOf* ?rootClass

     }

    FILTER(?isRoot)
} ORDER BY ?parentTerm
