prefix obo: <http://purl.obolibrary.org/obo/>
prefix obc: <http://www.pitt.edu/obc/>
prefix dc: <http://purl.org/dc/elements/1.1/>

select distinct ?hostTypeLabel ?hostType

#select distinct *  

from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
from <http://www.pitt.edu/obc/simulates-instances>


where {
 {
   ?dtm rdf:type <http://purl.obolibrary.org/obo/APOLLO_SV_00000561> . #dtm is dtm software

   #get compiledSoftware that corresponds to dtm via a compiling process
   ?compilingProcess obo:OBI_0000293 ?dtm .
   ?compilingProcess obo:OBI_0000299 ?executable .

   #get compiled software that achieves planned objective simulating of process type x
     ?simulatingOfProcessTypeX obo:OBI_0000417 ?executable . #achieves planned objective obo:OBI_0000417
     ?executable rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000525 . 
     ?simulatingOfProcessTypeX rdf:type obo:APOLLO_SV_00000005 .
 
   #get simulation of populations
     ?simulatingOfProcessTypeX obo:OBI_0000299 ?simulationOfPopulation .   #has specified output - obo:OBI_0000299
     ?simulationOfPopulation rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000031  .
     ?simulationOfPopulation obo:APOLLO_SV_00000067 ?population .
  
   # get (1) the host populations that are simulated, (2) their labels, (3) the type of host, and (4) the label for the host type.

      ?population rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000516 .
      #?population rdfs:label ?hostPopulationLabel .
      ?population a ?hostType .
      ?hostType rdfs:label ?hostTypeLabel 
 }
 UNION
 {
    ?dataset rdf:type/rdfs:subClassOf* obo:IAO_0000100 . #dataset is instance of data set
    ?dataset obo:IAO_0000136/obo:RO_0000057* ?population . #and it's either about population or something that has population as participant
    ?population rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000516 .
    ?population rdf:type ?hostType .
    ?hostType rdfs:label ?hostTypeLabel
 }
}
order by ?hostTypeLabel