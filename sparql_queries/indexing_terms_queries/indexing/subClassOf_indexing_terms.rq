PREFIX sp: <tag:stardog:api:property:>
PREFIX obo: <http://purl.obolibrary.org/obo/>


SELECT DISTINCT ?term ?termLabel ?parentTerm ?parentLabel ?rootClass ?rootLabel (rdfs:subClassOf as ?hieProp)
FROM <http://www.pitt.edu/obc/ide/apollo-sv>
FROM <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
FROM <http://www.pitt.edu/obc/ide/article-instances>
WHERE {
        ?class rdfs:subClassOf* ?rootClass .
        ?rootClass <http://www.pitt.edu/obc/IDE_0000000024> ?isRoot .
        ?rootClass rdfs:label ?rootLabel .
        ?class rdfs:label ?classLabel .
        ?rootClass <http://www.pitt.edu/obc/IDE_0000001000> rdfs:subClassOf .

        ?term rdf:type ?class .

        OPTIONAL {
                ?term rdfs:label ?termLabel .
                ?term rdf:type*/rdfs:subClassOf* ?parentTerm .
                ?parentTerm rdfs:label ?parentLabel .
                ?parentTerm rdfs:subClassOf+ ?rootClass
     }

    FILTER(?isRoot)
    MINUS { ?term rdf:type/rdfs:subClassOf* <http://purl.obolibrary.org/obo/IAO_0000088> } #don't want the reports
    MINUS { ?term rdf:type/rdfs:subClassOf* <http://purl.obolibrary.org/obo/IAO_0000311> } #don't want the publications
} ORDER BY ?parentTerm