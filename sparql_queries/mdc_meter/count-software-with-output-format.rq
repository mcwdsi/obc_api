prefix obo: <http://purl.obolibrary.org/obo/>
prefix dc: <http://purl.org/dc/elements/1.1/>

select (count (distinct ?x) as ?numberSoftwareWithOutputFormat) 
from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
where {
  ?x rdf:type ?softwareOrSubclass .
  ?softwareOrSubclass rdfs:subClassOf* obo:IAO_0000010 .
  ?x obo:IAO_0000111 ?prefTerm .
  ?v rdf:type obo:APOLLO_SV_00000525 .  #v is executable
  ?u rdf:type obo:APOLLO_SV_00000524 .  #u is compiling
  ?u obo:OBI_0000293 ?x .               #compiling has source code as input
  ?u obo:OBI_0000299 ?v .               #compiling has executable as output
  ?encodingPlan obo:BFO_0000050 ?v .		 #data encoding plan spec part of executable
  ?dataEncoding obo:OBI_0000417 ?encodingPlan . #encoding achieves encoding plan objective
  ?dataEncoding obo:OBI_0000417 ?outputSpec .  #encoding achieves spec objective
  ?dataEncoding rdf:type obo:OBI_0000227 .  #encoding individual is instance of encoding
  ?outputSpec rdf:type obo:IAO_0000098 . #output spec is data format spec
  #filter out punned individuals
     FILTER ( ?x != obo:IAO_0000010 )
     FILTER ( ?x != ?softwareOrSubclass )
     FILTER ( ?softwareOrSubclass != obo:APOLLO_SV_00000525 )
}