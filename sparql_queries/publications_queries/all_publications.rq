
PREFIX geo: <http://purl.obolibrary.org/obo/geo.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://www.obofoundry.org/ro/ro.owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>


SELECT DISTINCT ?information ?label ?title ?linkout ?date ?dateIndexed ?type ?typeLabel ?sourceOrganization ?authors ?pmid ?journal ?country ?version (group_concat(DISTINCT ?aboutLabel ; separator = " :: ") as ?abouts) ?doi ?pp ?os ?grant ?grantTitle ?pi
FROM <http://www.pitt.edu/obc/ide/apollo-sv>
FROM <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
FROM <http://www.pitt.edu/obc/ide/article-instances>
WHERE {
    ?information rdf:type ?type .
    ?type rdfs:subClassOf* <http://purl.obolibrary.org/obo/IAO_0000311> .
    ?type rdfs:label ?typeLabel .
    
    ?information obo:IAO_0000136 ?about .
    ?about rdfs:label ?aboutLabel .

    #used for string interpolation to filter based on search terms
    ##ABOUT##
    
    OPTIONAL { ?information rdfs:label ?label } .
    OPTIONAL { ?information <http://purl.obolibrary.org/obo/APOLLO_SV_00000325> ?dateIndexed } .
    OPTIONAL { ?information dc:title ?title } .
    OPTIONAL { ?information <http://purl.obolibrary.org/obo/ERO_0000480> ?linkout } .
    OPTIONAL { ?information <http://purl.org/dc/elements/1.1/date> ?date } .
    OPTIONAL { ?information <http://www.pitt.edu/obc/IDE_source_organization> ?sourceOrganization } .
    OPTIONAL { ?information <http://www.pitt.edu/obc/AUTHORS_1> ?authors } .
    OPTIONAL { ?information <http://purl.obolibrary.org/obo/OBI_0001617> ?pmid } .
    OPTIONAL {
        ?information <http://purl.obolibrary.org/obo/BFO_0000137> ?journalUri .
        ?journalUri dc:title ?journal .
    } .
    OPTIONAL { ?information <http://purl.org/dc/elements/1.1/coverage> ?country } .
    OPTIONAL { ?information <http://www.pitt.edu/obc/IDE_version> ?version } .
    
    OPTIONAL { ?information <http://www.pitt.edu/obc/IDE_0000000040> ?doi } .
    
    OPTIONAL { ?information obo:IAO_0000136 ?pp .
               ?pp rdf:type obo:OBI_0000011 .
               ?pp obo:OBI_0000417 ?os .
               ?os rdf:type obo:IAO_0000005 .
               ?grant obo:BFO_0000051 ?os .
               ?grant rdf:type ?grantType .
               ?grantType rdfs:subClassOf* obo:OBI_0001636 .
               ?grant dc:title ?grantTitle .
               ?grant obo:IDE_0000000171 ?pi .
    } .

} GROUP BY ?information ?label ?title ?linkout ?date ?dateIndexed ?type ?typeLabel ?sourceOrganization ?authors ?pmid ?journal ?country ?version ?doi ?pp ?os ?grant ?grantTitle ?pi


